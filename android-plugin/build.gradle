// build file for android-plugin component of the Zendesk Unity SDK

apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
    }
}

ext {
    getConfigurationParameter = { keyName ->
        return System.getenv(keyName) ?:
                rootProject.hasProperty(keyName) ? rootProject.properties[keyName] : ''
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://zendesk.artifactoryonline.com/zendesk/repo'}
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionName "2.2.1"
        vectorDrawables.useSupportLibrary = true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {

    /*
        This is the unity player which is hosted on our Artifactory server. Unity will fail to
        build if it is included locally because it results in duplicate definitions for particular
        Unity player activities.
     */
    compileOnly group: 'com.unity', name: 'player', version: '1.0.0.1'

    /*
     * The dependencies of the SDK artifact will be copied to the output directory in the packagePlugin
     * task.
     */
    implementation group: 'com.zendesk', name: 'support', version: '2.2.1'
}

apply plugin: 'maven'

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                snapshotRepository(url: "file:///tmp/repo")

                repository(url: "file:///tmp/repo")

                pom.project {
                    groupId 'com.zendesk'
                    artifactId 'unityplugin'
                    packaging 'aar'
                    version '2.2.1'

                    licenses {
                        license {
                            name 'Zendesk Master Subscription Agreement'
                            url 'https://www.zendesk.com/company/customers-partners/#master-subscription-agreement'
                            distribution 'repo'
                            comments 'By downloading or using the Zendesk Mobile SDK, You agree to the Zendesk Master Subscription Agreement https://www.zendesk.com/company/customers-partners/#master-subscription-agreement and Application Developer and API License Agreement https://www.zendesk.com/company/customers-partners/#application-developer-api-license-agreement and acknowledge that such terms govern Your use of and access to the Mobile SDK.'
                        }

                        license {
                            name 'Application Developer and API License Agreement'
                            url 'https://www.zendesk.com/company/application-developer-and-api-license-agreement'
                            distribution 'repo'
                            comments 'By downloading or using the Zendesk Mobile SDK, You agree to the Zendesk Master Subscription Agreement https://www.zendesk.com/company/customers-partners/#master-subscription-agreement and Application Developer and API License Agreement https://www.zendesk.com/company/customers-partners/#application-developer-api-license-agreement and acknowledge that such terms govern Your use of and access to the Mobile SDK.'
                        }
                    }
                }
            }
        }
    }
}
