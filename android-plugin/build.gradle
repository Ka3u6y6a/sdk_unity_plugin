// build file for android-plugin component of the Zendesk Unity SDK

apply plugin: 'com.android.library'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

repositories {
    jcenter()
    maven { url 'https://zendesk.artifactoryonline.com/zendesk/repo'}
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionName "1.7.0.1"
        vectorDrawables.useSupportLibrary = true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {

    /*
        This is the unity player which is hosted on our Artifactory server. Unity will fail to
        build if it is included locally because it results in duplicate definitions for particular
        Unity player activities.
     */
    compile group: 'com.unity', name: 'player', version: '1.0.0.1'

    /*
        Any jar dependencies should be placed in libs. this will result in the Zendesk Unity plugin
        being generated as an aar with this inlined. It's a partially fat AAR.
     */
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /*
        This version of the Zendesk SDK should match the versions in plugin-dependencies. For example,
        if this version is a.b.c.d, then plugin dependencies should contain:

        sdk-a.b.c.d.aar
        sdk-providers-a.b.c.d.aar
     */
    compile group: 'com.zendesk', name: 'sdk', version: '1.7.2.1'
}

task packagePlugin <<  {

    // AAR dependencies
    copy {
        from "$rootDir/android-plugin/plugin-dependencies"
        into "$rootProject.buildDir/unity-plugin/Plugins/Android"
    }

    // Strips css from sdk aar artifact, replaced by the css file in src/main/assets
    tasks.stripCssFromAndroidSdk.execute()
    print tasks.stripCssFromAndroidSdk.output()

    // The aar from this project
    copy {
        from "$buildDir/outputs/aar/$project.name-release.aar"
        into "$rootProject.buildDir/unity-plugin/Plugins/Android"
    }
}

/**
 * If a non-gradle build system is being used within Unity, then we have to remove the Guide CSS
 * file from the sdk artifact. AAPT will fail if we do not do this. bash -c is being invoked
 * because we want the sdk artifact to be globbed to prevent removing css files from other
 * artifacts.
 *
 * This is unlikely to be required when using the gradle build system, which is an option in
 * Unity 5.5 and later.
 *
 * This will not work on Windows. If you are trying to build on Windows, then do not call this
 * task from packagePlugin, and instead delete 'assets/help_center_article_style.css' from
 * $rootProject.buildDir/unity-plugin/Plugins/Android/sdk-a.b.c.d.aar
 */
task stripCssFromAndroidSdk(type:Exec) {

    /*
        ignoreExitValue can be uncommented to get detailed error messaging if the zip task fails.
        If the code is 12 then it is likely that zip cannot find the file, or the file has already
        had the css file removed from it.
    */
    // ignoreExitValue true

    workingDir "$rootProject.buildDir/unity-plugin/Plugins/Android"
    commandLine 'bash', '-c', 'zip -d sdk-[0-9].[0-9].[0-9].[0-9].aar assets/help_center_article_style.css'

    standardOutput = new ByteArrayOutputStream()

    ext.output = {
        return standardOutput.toString();
    }
}

packagePlugin.dependsOn('clean')
assemble.dependsOn packagePlugin

